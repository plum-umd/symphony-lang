principal A B

-- CCS21 BENCHMARK

-- library of option types as tagged pairs:
def tagJust v = (true, v)

def tagNothing = (false, (0,0,0))

def elim-ooption v vNothing vJust = mux if (fst v)
  then vJust (snd v)
  else vNothing

def main () = par {{A,B}}
  let inputA  = par {{A}} read list (ℤ × ℤ) from "db-analytics-input-{1}.txt" in
  let publicA = send-ls {{A}} {{A,B}} inputA in
  let sharedA = map (fun x → share {{{0}, ℤ × ℤ : A → A,B}} x) publicA in

  let inputB  = par {{B}} read list (ℤ × ℤ) from "db-analytics-input-{1}.txt" in
  let publicB = send-ls {{B}} {{A,B}} inputB in
  let sharedB = map (fun x → share {{{0}, ℤ × ℤ : B → A,B}} x) publicB in

  -- compute facts about the union of DB's:
  let union-db = append sharedA sharedB in
  let col2 = map snd union-db in
  let union-num-elts = length union-db in

  let union-mean = (sum-ints col2) / union-num-elts in
  let union-variance = (elim-list col2 0
    (fun x -> let d = x - union-mean in plus (d * d))) /
    union-num-elts in

  -- compute facts about the join of DB's:
  let join-db = elim-list sharedA [ ]
    (fun (xa, ya) join-db0 -> elim-list sharedB join-db0
      (fun (xb, yb) -> cons
         (mux if xa == xb then tagJust (xa, ya, yb)
          else tagNothing)
      )) in

  let join-num-elts = elim-list join-db 0
    (fun x sum -> elim-ooption x sum (const (plus 1 sum))) in
  let join-mean = (elim-list join-db 0
    (fun x -> plus (elim-ooption x 0 (fun (x0, y0, z0) -> y0))) ) /
    join-num-elts in
  let join-variance = (elim-list join-db 0
    (fun x -> plus (elim-ooption x 0
      (fun (x0, y0, z0) -> let d = y0 - join-mean in (d * d)) ) ) ) /
    join-num-elts in

  (reveal {{{0}, ℤ : A,B → A,B }} union-mean,
   reveal {{{0}, ℤ : A,B → A,B }} union-variance,
   reveal {{{0}, ℤ : A,B → A,B }} join-mean,
   reveal {{{0}, ℤ : A,B → A,B }} join-variance)
