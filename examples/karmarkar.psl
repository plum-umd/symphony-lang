-- BEGIN definitions appropriate for general-purpose library modules

-- TODO: define these properly
def log_2_real = id

def magn x = 1n

-- order: alias for list length
def order = len

-- eq-vecs: alias for vectors
def eq-vecs = eq_lists

-- minux-vecs: alias for subtracting lists
def minus-vecs = minus-lists

-- row-of-ones: a vector of real ones
def row-of-ones : nat -> list real
def row-of-ones = const-ls 1.0

-- BEGIN: application-specific code

-- m : user parameter: number of constraint parties
def m : nat
def m = 8n

-- n : user parameter: dimension of the constraint system
def n : nat
def n = 8n

-- Ctr[m]: parties that bring the constraints
principal Ctr[8]

-- party that provides the cost vector:
principal Cost

-- party that gets the output:
principal Out

-- comps: the parties that do computation, Ctr and Cost
-- def comps = set(Ctr,Cost)
def comps = { Ctr, Cost }

-- shr-w-comp x: x, SPDZ-shared with the computation parties
def shr-w-comp sender x = share{ spdz : sender -> comps } x

-- shr0, shr1: shares of 0.0 and 1.0 between the computation parties
def shr0 = shr-w-comp { Cost } 0.0 
def shr1 = shr-w-comp { Cost } 1.0 
     
def iter_sec_nat_spdz muxer = iter_sec_nat muxer 

def iter_sec_nat_ls = iter_sec_nat_spdz mux_ls

-- nz_prod z u l: the product of non-z entries in l, with
-- multiplicative unit u
def nz-prod z u l = elim_list l
  u
  (fun x p -> mux if (x == z) then x else x * p)

def nz-prod-flt = nz-prod 0.0 1.0

-- nz-prod-mat z u m: product of non-z entries in matrix m with
-- multiplicative unit u.
def nz-prod-mat z u m =
  let nz-prod-ls = nz-prod z u in
  nz-prod-ls (map nz-prod-ls m)

def nz-prod-mat-flt = nz-prod-mat 0.0 1.0

def zero-vec = zero-ls-flt

def zero-vec-shrs n = map (shr-w-comp { Cost }) (zero-vec n)

-- range m n: the list of elements from m to n
def range m n =
  if n < m then [ ]
  else map (plus m)
    (elim_nat (n - m)
       [ 0n ]
       (fun rng -> snoc (len rng) rng) )

-- Listing 9: Inverse of a lower triangular matrix
def matrix_inverse_lt l =
  let n = order l in -- order of L
  let l_inv = ref (zero-mat n) in -- n x n zero matrix
  do (iter (range 0n (n - 1n))
    (fun k ->
      -- For diagonal entries, take the reciprocal
      do l_inv := upd-matrix !l_inv (k, k) (shr1 / (get-matrix !l_inv (k, k))) in

      -- For non-diagonal entries, solve L * L_inv = I
      iter (range (k + 1n) (n - 1n))
        (fun j ->
          let sum = ref shr0 in
          iter (range k (j - 1n))
            (fun i ->
              sum := !sum - (get-matrix l (j, i)) * (get-matrix !l_inv (i, k))
              ) ) ) ) in
  !l_inv


-- Listing 10: Cholesky decomposition of a positive definite Hermitian
-- matrix
def cholesky_decomposition m =
  -- Initialize variables
  let n = order m in -- order of m
  let l = ref (zero-mat n) in -- n x n zero matrix

  -- Calculate the top diagonal element of L
  do l := upd-matrix !l (0n, 0n) (sqrt (get-matrix m (0n, 0n))) 

  -- Calculate the rest of the first column of L
  do iter (range 1n (n - 1n))
    (fun j ->
      l := upd-matrix !l (j, 0n) ((get-matrix m (j, 0n)) / (get-matrix !l (0n, 0n)) ) )

  -- Iterate through the remaining columns
  do iter (range 1n (n - 1n))
    (fun i ->
      -- First calculate the diagonal element for this column
      let sum = ref shr0 in
      do iter (range 0n (i - 1n))
        (fun j ->
          sum := !sum + (get-matrix !l (i, j)) * (get-matrix !l (i, j)) ) 
      do l := upd-matrix !l (i, i) (sqrt ((get-matrix m (i, i)) - !sum)) in
      -- Now fill in the remaining entries in this column
      if-then (i < n - 1n) 
        (iter (range (i + 1n) (n - 1n))
          (fun j ->
            do sum := shr0 in
            iter (range 0n (i - 1n))
              (fun k ->
                sum := !sum + (get-matrix !l (i, k)) * (get-matrix !l (j, k)) ) ) ) ) in
    !l

-- Listing 8: Inverse of a positive definite Hermitian matrix
def matrix_inverse_pdh : num -> matrix -> matrix
def matrix_inverse_pdh m =
  let l = cholesky_decomposition m in
  let l_inv = matrix_inverse_lt l in -- Invert the lower-triangular matrix
  let m_inv = mult-matrices-flt (transpose l_inv) l_inv in
  m_inv

-- Listing 6: Iteration for Karmarkar's Algorithm
def karmarkar_iteration initial_point a c =
  let (m, n) = dimensions_of a in
  -- D is a matrix of zeros with the values of initial_point on the
  -- diagonal
  -- Dimensions of D: n x n
  let d = diagonal-matrix-flt initial_point in
  -- Dimensions of cprime: n x 1
  let cprime = mult-matrix-vec-flt d c in
  -- Dimensions of AD: m x n
  let ad = mult-matrices-flt a d in

  -- Construct B
  -- Dimensions of B: (m + 1) x n
  let b = append ad [ row-of-ones n ] in -- append a row of ones onto AD

  -- Dimensions of BBT: (m + 1) x (m + 1)
  let bbt = mult-matrices-flt b (transpose b) in

  -- Dimensions of BBTInv: (m + 1) x (m + 1)
  let bbtinv = matrix_inverse_pdh bbt in

  -- Project C onto B's nullspace
  -- Dimensions of cp: n x 1
  let cp = minus-vecs cprime
    (mult-matrix-vec-flt (transpose b)
      (mult-matrix-vec-flt bbtinv
        (mult-matrix-vec-flt b cprime) ) ) in

  -- Calculate a unit vector of cp
  -- Dimensions of unitcp: n x 1
  let unit_cp =
    mux_ls (eq-vecs cp (zero-vec n)) -- If cp is the zero vector
      -- If cp is the zero vector, assign it to be 0
      (zero-vec-shrs n)
      (div-scalar-vec (zero-vec n) (l2norm cp)) in

  -- Take a step of 1/3n in the direction of -cp, row-of-ones(n)/n is
  -- the "transformed" initial point
  -- Dimensions of bprime: n x 1
  let bprime = minus-vecs
    (div-scalar-vec (row-of-ones n) (to_flt n))
    (mult-scalar-vec (1.0 / (3.0 * (to_flt n))) unit_cp) in

  -- Transform the point back
  -- Dimensions of b: n x 1
  let b = div-scalar-vec (mult-matrix-vec-flt d bprime)
    (dot-prod-flt (row-of-ones n) (mult-matrix-vec-flt d bprime)) in
  b

-- Listing 7: Loop for Karmarkar's Algorithm
def karmarkar_loop a c =
  let (m, n) = dimensions_of a in
  -- Epsilon is the stopping condition. This value should be close to
  -- 0, so we choose 10^(-8)
  let epsilon = 10.0 ^ -8.0 in

  -- x is our initial point
  let x = ref (div-scalar-vec (row-of-ones n) (to_flt n)) in

  -- product of non-zero entries in c and A
  let p = (nz-prod-flt c) * (nz-prod-mat-flt a) in
  let l = (m + 1n) * n +
    (magn (ceil (log_2_real (abs-val-flt-shr p)))) +
    n * (magn (ceil (log_2_real (to_flt n)))) in
  let max_iterations = 12n * n * l in

  -- If this initial point is an optimal solution, return
  mux_ls ((dot-prod-flt c !x) < epsilon) !x
  -- hand-computed bound
  (iter_sec_nat_ls max_iterations 1n (fun i -> 
    let x_new = karmarkar_iteration !x a c in cont_seq'

    -- if we are within the stopping condition in the current
    -- iteration, return
    (if-then-ret-ls 
      ((((dot-prod-flt c !x) - (dot-prod-flt c x_new)) /
         (max-muxable shr1 (dot-prod-flt c !x)) ) < epsilon )
      x_new)
    (x := x_new) )
  x)

-- main: entry point; Ctr, Cost read inputs and run the algorithm,
-- distributes results to Out
def main : unit ->{ inp : Ctr, Cost; rev : Out } (list out_sec)
def main () =
  -- read_vec: reads an input vector on a principal's machine
  -- let read_vec = fun () -> read (list flt) "in.txt" in
  -- TODO: replace with file input
  let read_vec = fun () -> [ 1.0 ; 2.0; 3.0; 4.0; 5.0; 6.0; 7.0; 8.0 ] in 

  -- FANCY MPC: Ctr and Cost send the data in the list to Out. This
  -- may change if the PSL model of isec's changes as a result of
  -- issue #31
  let soln = par { Ctr, Cost, Out } (map
    (reveal-wrap { Out })
    -- Cost reveals the structure of the list to out:
    (send-ls { Cost } { Ctr, Cost, Out }
      -- Ctr and Cost compute the solution point:
      (par { Ctr, Cost } karmarkar_loop
        -- read in the matrix of constraints and share them
        (fold-f
          [ ]
          (fun Q ctr -> cons (map (shr-w-comp { Q }) (send-ls { Q } comps ctr)))
          (solo { Ctr } as c in read_vec ()) )
        -- read in the vector of costs and share them
        (map (shr-w-comp { Cost })
          (send-ls { Cost } comps (par { Cost } read_vec ())) ) ) ) ) in
  -- Ctr and Cost forget the structure of the solution
  par { Out } send-ls { Out } { Out } soln
